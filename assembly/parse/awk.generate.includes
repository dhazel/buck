#!/bin/awk

BEGIN {
    
}
#-------------------------------------------------------------------------------
{
    # read in the dependancy file and parse it into a simple database
    if(FILENAME ~ /^dependancies$/){
        if(/\./){
            # table 1: the file path data
            filenumber = filenumber + 1
            fileroutinenumber = 1
            pathtable[filenumber,1] = $1
        }else if(/:/){
            fileroutinenumber = fileroutinenumber + 1 
            pathtable[filenumber,fileroutinenumber] = $1

            # table 2: the dependancy data
            routinenumber = routinenumber + 1
            depnumber = 1
            deptable[routinenumber,1] = $1
        }else if(/[a-zA-Z0-9]/){
            depnumber = depnumber + 1
            deptable[routinenumber,depnumber] = $1
        }
    }

    # read in the input filenames (that's all we need)
    if(file != FILENAME){
        file = FILENAME
        if(FILENAME !~ /^dependancies$/){
            inputfilenumber = inputfilenumber + 1
            filearray[inputfilenumber] = file
        }
    }
}
#-------------------------------------------------------------------------------
END {
    print " -- "
    if(!pathtable[1,1]){
        print "FAILURE: no \"dependancies\" file included with input... exiting"
        print " -- "
        exit 1
    }
    extension = ".inc"

    # generate the include files
    file = 1
    for(file in filearray){
        # clear the storage arrays
        delete routinearray
        delete deppatharray

        # write the file header
        includefile = filearray[file] extension
        print "; "filearray[file]" include file --------------------------"\
                                                    > includefile
        print ";    ***automatically generated***"  > includefile
        print ";"                                   > includefile

        ### map the dependancies to their paths
        # find the file in the pathtable
        i = 1
        while(pathtable[i,1] !~ filearray[file]){
            i = i + 1      
        }

        print filearray[file] extension

        # generate the file's routine array (the routines in the file)
        row = i
        col = 2
        routine_i = 1
        while(pathtable[row,col]){
            routinearray[routine_i] = pathtable[row,col]
            routine_i = routine_i + 1
            col = col + 1
        }

        # generate the file's dependancy and dependancy path arrays
        routine = 1
        while(routinearray[routine]){
            # find the routine in the deptable
            i = 1
            while((deptable[i,1] !~ "^"routinearray[routine]"$") && \
                                                            (deptable[i,1])){
                i = i + 1
            }
            if(! deptable[i,1]){
                print " WARNING: missing path for \""routinearray[routine]"\""
                print "     to fix, add a formatted note to whichever file it \
belongs."
            }

            # copy dependancies to the end of the routinearray
            row = i
            col = 1
            while(deptable[row,col]){
                col = col + 1

                # check for repeats (do not copy them)
                copy = 1
                element = 1
                while((routinearray[element]) && (copy == 1)){
                    if(deptable[row,col] ~ "^"routinearray[element]"$"){
                        copy = 0
                    }
                    element = element + 1
                }
                if(copy){
                    routinearray[element] = deptable[row,col]
                }
            }

            # find the routine in the path table
            row = 1
            col = 1
            end_search = 0
            while((pathtable[row,col]) && (end_search != 1)){
                col = col + 1           # at col == 2 the element may be null
                while((pathtable[row,col]) && (end_search != 1)){
                    if(pathtable[row,col] ~ "^"routinearray[routine]"$"){
                        end_search = 1
                        row = row - 1   # compensates for the addition below
                    }
                    col = col + 1
                }
                col = 1
                row = row + 1
            }

            # copy routine path into the dependancy path array
                # check for repeats (do not copy them)
            copy = 1
            element = 1
            while((deppatharray[element]) && (copy == 1)){
                if(deppatharray[element] ~ "^"pathtable[row,1]"$"){
                    copy = 0
                }
                element = element + 1
            }
            if(copy){
                deppatharray[element] = pathtable[row,1]
            }
            routine = routine + 1
        }

        # write the dependancy paths to the include file
        element = 1
        for(element in deppatharray){
            if((deppatharray[element] ~ /[a-zA-Z0-9]/) && \
                                    (deppatharray[element] !~ filearray[file])){
                print "#include "deppatharray[element]  > includefile
            }
        }
    }
    print " -- "
    print "done"
    print " -- "
}
